WHITESPACE    = _{ " " | "\t" | NEWLINE }
identifier    = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
symbol        = @{ ":" ~ identifier }
int           = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
float         = @{ ("+" | "-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
bool          = @{ "true" | "false" }
fqid          =  { identifier ~ ("." ~ identifier)* }
function      =  { fqid ~ "(" ~ (exp ~ ("," ~ exp)*)? ~ ")" }
string_escape = @{ "\\\"" | "\\\\" }
string_elem   = @{ (!"\"" ~ !string_escape ~ ANY)+ }
string        = ${ "\"" ~ (string_escape | string_elem)* ~ "\""}
variable      =  { fqid }
array         = { "[" ~ (exp ~ ("," ~ exp)*)? ~ "]" }
exp           =  { bool | function | variable | array | string | symbol | float | int }
subst         = !{ "{{" ~ exp ~ "}}" }
text_escape   = @{ "\\{" | "\\\\" | "\\$" }
text_elem     = @{ (!"{{" ~ !text_escape ~ ANY)+ }
text          = ${ (text_escape | text_elem)  }

expand        =  { SOI ~ "${" ~ exp ~ "}" ~ EOI }
template      = ${ SOI ~ (text | subst)* ~ EOI }

syntax        =  { expand | template }